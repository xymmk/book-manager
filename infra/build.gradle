buildscript {
    dependencies {
        classpath("org.flywaydb:flyway-database-postgresql:10.10.0")
    }
}

plugins {
    id 'org.jooq.jooq-codegen-gradle' version "3.19.11"
    id 'org.flywaydb.flyway' version "10.10.0"
}

dependencies {
    implementation(project(':domain'))
    implementation("org.springframework.boot:spring-boot-starter-jooq")
    implementation 'org.springframework:spring-tx:6.2.2'
    runtimeOnly("org.postgresql:postgresql")
    implementation("org.jooq:jooq:3.19.11")
    implementation("org.jooq:jooq-meta:3.19.11")
    implementation("org.jooq:jooq-codegen:3.19.11")
    implementation("org.jooq:jooq-postgres-extensions:3.19.11")
    jooqCodegen("org.postgresql:postgresql:42.7.4")
}

jooq {
    configuration {

        jdbc {
            driver = "org.postgresql.Driver"
            url = System.getenv('POSTGRES_DB') ?: "jdbc:postgresql://localhost:5432/book-manager"
            user = System.getenv('POSTGRES_USER') ?: "postgres"
            password = System.getenv('POSTGRES_PW') ?: "postgres"
        }
        generator {
            name = 'org.jooq.codegen.KotlinGenerator'
            database {
                name = 'org.jooq.meta.postgres.PostgresDatabase'
                inputSchema = 'public'
                includes = ".*"
                excludes = "flyway_schema_history"
            }
            generate {
                deprecated = false
                kotlinSetterJvmNameAnnotationsOnIsPrefix = true
                pojosAsKotlinDataClasses = true
                fluentSetters = true
                interfaces = true
            }
            target {
                packageName = "com.quo.book.manager.jooq"
                directory = 'src/generated'
            }
        }
    }
}

sourceSets {
    main {
        kotlin.srcDir("src/generated")
    }
}

def enableJooqCodegen = project.hasProperty('enableJooqCodegen') && project.property('enableJooqCodegen') == 'true'

if (enableJooqCodegen) {
    tasks.named("compileKotlin") {
        dependsOn(tasks.named("jooqCodegen"))
    }
}

flyway {
    driver = "org.postgresql.Driver"
    //docker環境でビルドする場合、環境変数から取得し、コンテナのDBに接続する
    url = System.getenv('POSTGRES_DB') ?: "jdbc:postgresql://localhost:5432/book-manager"
    user = System.getenv('POSTGRES_USER') ?: "postgres"
    password = System.getenv('POSTGRES_PW') ?: "postgres"
    schemas = ["public"]
    cleanDisabled = false
}

tasks.named("jooqCodegen") {
    dependsOn(tasks.named("flywayMigrate"))
    inputs.files(fileTree("src/main/resources/db/migration"))
}

flywayMigrate.dependsOn flywayRepair

tasks.register("startPostgresql") {
    doLast {
        def active = System.getenv("spring.profiles.active")
        println "Running in $active mode, executing script..."
        if (active == "test") {
            exec {
                commandLine 'sh', 'startup_postgresql.sh'
            }
        }
    }
}


flywayMigrate.dependsOn tasks.named("flywayRepair")
flywayRepair.dependsOn tasks.named("startPostgresql")
